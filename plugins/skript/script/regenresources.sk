#Depend:skUtilities
options:
    regendate: 3    #月0火1水2木3金4土5日6
    regentime: 1:33
    numberofworld: 4
    worldidprefix: sigen
    worldnameprefix:資源
    schematicname:sigenspawn
#Variables
#{{@worldidprefix}worldcount} これまでの再生成したワールド数:number
#{{@worldidprefix}disablegate} 資源へのゲートを利用可能にするか:boolean
#loop {@numberofworld} times:
#   set {{@worldidprefix}worldtogate::%{{@worldidprefix}worldcount}-loop-value+1%} to loop-value    set {sigenworld}
#   set {{@worldidprefix}gatetoworld::%loop-value%} to {{@worldidprefix}worldcount}-loop-value+1
#{{@worldidprefix}gatetoworld::*}  Gate to world    {sigengatetoworld::4} = 1   ゲート4は資源1につながる
#{{@worldidprefix}worldtogate::*}  world to gate    {sigenworldtogate::1} = 4   資源1はゲート4につながる
#
#region
#{{@worldidprefix}gatein::*}
#sigengatein1
#sigengatein2
#sigengatein3...
#{{@worldidprefix}gateout::*}
#sigengateout1
#sigengateout2
#sigengateout3...
on skript load:
    if {{@worldidprefix}worldcount} isn't set:
        set {{@worldidprefix}worldcount} to {@numberofworld}
every minute in "world":
    if "%now%" contains "{@regentime}":
        if "%date({@regendate})%" is "true":
            add 1 to {{@worldidprefix}worldcount}    #5
            set {_removeworldnumber} to {{@worldidprefix}worldcount}-{@numberofworld} #1
            set {{@worldidprefix}disablegate::%{_removeworldnumber}%} to true    #{disablegate::1} to true

            set {_removeworldid} to "{@worldidprefix}%{_removeworldnumber}%"  #"sigen1"
            set {_newworldid} to "{@worldidprefix}%{{@worldidprefix}worldcount}%"  #"sigen5"
            set {_removeworldname} to "{@worldnameprefix}%{_removeworldnumber}%"  #"資源1"
            set {_newworldname} to "{@worldnameprefix}%{{@worldidprefix}worldcount}%"  #"資源5"

            broadcast "60秒後に%{_removeworldname}%を削除し，%{_newworldname}%の生成を行います。"
            wait 30 seconds
            broadcast "30秒後に%{_removeworldname}%を削除し，%{_newworldname}%の生成を行います。"
            wait 20 seconds
            loop 10 times:
                broadcast "%10-loop-value%秒前"
                wait 1 seconds
            broadcast "生成を開始するため%{_removeworldname}%のプレイヤーをテレポートしました"
            loop all players:
                if loop-player's world is "%{_removeworldid}%":
                    execute loop-player command "/spawn"
            execute console command "/mv unload %{_removeworldid}%"
            wait 3 seconds
            if file "plugins\regenresorce\{@worldidprefix}backup" isn't a file:
                create dir "plugins\regenresorce\{@worldidprefix}backup"
            zip directory "%{_removeworldid}%" to zip "plugins\regenresorce\{@worldidprefix}backup\%{_removeworldid}%.zip"
            execute console command "/mv load %{_removeworldid}%"
            execute console command "/mv delete %{_removeworldid}%"
            execute console command "/mv confirm"
            execute console command "/mv create %{_newworldid}% normal"
            wait 5 seconds
            loop {@numberofworld} times:
                set {_countnumber} to {{@worldidprefix}worldcount} - loop-value
                add 1 to {_countnumber}
                set {{@worldidprefix}worldtogate::%{_countnumber}%} to loop-value
                set {{@worldidprefix}gatetoworld::%loop-value%} to {_countnumber}
            set {_newgatenumber} to {{@worldidprefix}worldtogate::%{_newworldid}%}
            copy file "plugins\regenresorce\regions.yml" to "plugins\WorldGuard\worlds\%{_newworldid}%"
            set line 13 in file "plugins\WorldGuard\worlds\%{_newworldid}%\regions.yml" to "    {@worldidprefix}gateout%{_newgatenumber}%:"
            execute console command "/rg reload"
            execute console command "/wb %{_newworldid}% set 50 spawn"
            execute console command "/wb %{_newworldid}% fill 500 10 false"
            execute console command "/wb fill confirm"
            wait 3 seconds
            execute console command "/wb %{_newworldid}% set 2000 spawn"
            set {{@worldidprefix}newworld::%{_newgatenumber}%} to true
on region enter:
    wait 1 tick
    if "%region at player%" contains "{@worldidprefix}gatein":
        if {gateowner} is the owner of the region:
            set {_outvalue::*} to split "%region at player%" by "gatein"
            set {_outnumber} to {_outvalue::3}
            if "%region at player%" contains "{@worldidprefix}gatein%{_outnumber}%":
                if {{@worldidprefix}disablegate::%{_outnumber}%} is true:
                    message "生成中のためワールド内に入ることができません．"
                else if {{@worldidprefix}newworld::%{_outnumber}%} is true:
                    set {{@worldidprefix}newworld::%{_outnumber}%} to false
                    execute console command "/mvtp %player% {@worldidprefix}%{{@worldidprefix}gatetoworld::%{_outnumber}%}%"
                    wait 10 tick
                    set {{@worldidprefix}location::%{{@worldidprefix}gatetoworld::%{_outnumber}%}%} to player's location
                    set y coordinate of {{@worldidprefix}location::%{{@worldidprefix}gatetoworld::%{_outnumber}%}%} to 65
                    set x coordinate of {{@worldidprefix}location::%{{@worldidprefix}gatetoworld::%{_outnumber}%}%} to 0
                    set z coordinate of {{@worldidprefix}location::%{{@worldidprefix}gatetoworld::%{_outnumber}%}%} to 0
                    teleport the player to {{@worldidprefix}location::%{{@worldidprefix}gatetoworld::%{_outnumber}%}%}
                    make player run cmd "//schem load {@schematicname}" as op
                    make player run cmd "//paste" as op
                    set z coordinate of {{@worldidprefix}location::%{{@worldidprefix}gatetoworld::%{_outnumber}%}%} to -3
                else:
                    message "teleported"
                    teleport the player to {{@worldidprefix}location::%{{@worldidprefix}gatetoworld::%{_outnumber}%}%}
    else if "%region at player%" contains "{@worldidprefix}gateout":
        if {gateowner} is the owner of the region:
            set {_outvalue::*} to split "%region at player%" by "gateout"
            set {_outnumber} to {_outvalue::3}
            message "teleported"
            teleport the player to {{@worldidprefix}inlocation::%{{@worldidprefix}worldtogate::%{_outnumber}%}%}

command /setgatelocation <number>:
    permission: regenresourse.setspawn
    description: Set resourse spawn
    trigger:
        set {{@worldidprefix}inlocation::%arg-1%} to player's location

command /setgateowner:
    permission: regenresourse.setowner
    description: Set owner spawn
    trigger:
        set {gateowner} to the player
        message "Set owner %{gateowner}%"

function date(n:number) :: boolean: #月0火1水2木3金4土5日6
    set {_now} to "%now%"
    set {_date.time::*} to {_now} split at " "

    set {_date::*} to {_date.time::1} split at "/"
    set {_time::*} to {_date.time::2} split at ":"

    set {_time.hour} to {_time::1}
    set {_time.minute} to {_time::2}
    
    set {_date.day} to {_date::3} parsed as number
    set {_date.month} to {_date::2} parsed as number
    set {_date.year} to {_date::1} parsed as number
    add 2000 to {_date.year}
    if {_date.month} <= 2:  #2月以下なら年から1引いて月に13足す
        set {_date.year} to {_date.year} - 1
        set {_date.month} to {_date.month} + 13
    else:   #それ以外は月に1足す
        set {_date.month} to {_date.month} + 1
    set {_A} to rounded down (365.25*{_date.year})
    set {_B} to rounded down (30.6*{_date.month})
    set {_C} to rounded down ({_date.year}/400) 
    set {_D} to rounded down ({_date.year}/100)
    set {_E} to ({_A}+{_B}+{_C}+{_date.day}-{_D})-429
    set {_F} to rounded down ({_E}/7)
    set {_F} to {_F}*7
    set {_G} to {_E} - {_F}
    if {_G} is {_n}:
        set {_result} to true
    else:
        set {_result} to false
    return {_result}
